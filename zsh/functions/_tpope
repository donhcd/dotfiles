#compdef tpope launch x-terminal-emulator logview lv xauth-get sursync

local tmp

case $service in
  tpope)
    if [[ $CURRENT = 2 ]]; then
      tmp=($(tpope aliases) activity away start stop restart reload force-reload 'runcontrol:start or stop daemons' rclink 'launch:start and disown a process' 'media:multimedia functions' 'sensible-browser:web browser' 'x-terminal-emulator:X terminal emulator' 'xauth-get:retrieve X authorization' 'ruptime:remote uptime' 'rwho:remote who' 'ps-grep:search processes' 'clean:clean up temporary and backup files' 'sursync:rsync as root' 'temperature:display local temperature' "tomorrow:tomorrow\'s MHC schedule" 'word:grep /usr/share/dict/words' 'logview:View system logs' install)
      _describe -t commands 'tpope command' tmp --
    else
      shift words
      (( CURRENT-- ))
      curcontext="${curcontext%:*:*}:tpope-${words[1]}:"
      [[ $words[1] = link ]] && words[1]=rclink
      [[ $words[1] = sursync ]] && words[1]=rsync
      [[ $words[1] = tomorrow ]] && words[1]=today
      [[ $words[1] = mpl ]] && words[1]=mplayer
      [[ $words[1] = mpa ]] && words[1]=mplayer
      [[ $words[1] = pg || $words[1] = ps-grep ]] && words[1]=grep
      [[ $words[1] = launch ]] && shift words && (( CURRENT-- ))
      _normal
    fi ;;
  logview|lv)
    _description files expl 'log file'
    _files "$@" "$expl[@]" -W /var/log -g "*~*.(gz|old|*[0-9])(-.)"
    ;;
  x-terminal-emulator)
    words[1]=xterm
    _normal ;;
  xauth-get)
    _hosts ;;
esac
